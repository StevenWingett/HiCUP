#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long;
use FindBin '$Bin';
use lib "$Bin/../";
use lib "$Bin/"; #Useful for ln -s to script, but need hicup_module.pm in same dir
use hicup_module;
use hicup_module qw(get_csome_position); 

########################################################################################
########################################################################################
## This file is Copyright (C) 2021, Steven Wingett (steven.wingett@mrc-lmb.cam.ac.uk) ##
##                                                                                    ##
##                                                                                    ##
## This file is part of HiCUP.                                                        ##
##                                                                                    ##
## HiCUP is free software: you can redistribute it and/or modify                      ##
## it under the terms of the GNU General Public License as published by               ##
## the Free Software Foundation, either version 3 of the License, or                  ##
## (at your option) any later version.                                                ##
##                                                                                    ##
## HiCUP is distributed in the hope that it will be useful,                           ##
## but WITHOUT ANY WARRANTY; without even the implied warranty of                     ##
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the                      ##
## GNU General Public License for more details.                                       ##
##                                                                                    ##
## You should have received a copy of the GNU General Public License                  ##
## along with HiCUP.  If not, see <http://www.gnu.org/licenses/>.                     ##
########################################################################################
########################################################################################


##########################################################
#Get user-supplied parameters
#Option variables
my %config = (
  help            => '',
  version => '',
  zip => ''
);


my $config_result = GetOptions(
    "help"        => \$config{help},
    "version"     => \$config{version},
	"zip" => \$config{zip}
);
die "Could not parse options.\n" unless ($config_result);


if ( $config{help} ) {
    print while (<DATA>);
    exit(0);
}

if ( $config{version} ) {    #Print version and exit
    print "hicup2homer v$hicup_module::VERSION\n";
    exit(0);
}

unless(@ARGV){
    print while (<DATA>);
    exit(0);
}


##########################################################
#Parse data
my @files = @ARGV;    #Pass file names as command-line arguments
foreach my $file (@files){
  
  chomp $file;
  print "Processing $file\n";
  
  if ($file =~ /\.gz$/){
    open (IN,"gunzip -c $file |") or die "Couldn't read $file : $!";  
  }
  elsif ($file =~ /\.bam$/) {
    open (IN,"samtools view $file |") or die "Couldn't read $file: $!";
  }
  else{
    open (IN, $file) or die "Could not open $file\n";
  }
  
  my $outfile = "$file.homer";
  if($config{zip}){
	$outfile .= '.gz';
	open(OUT, "| gzip -c - > $outfile") or die "Could not write to '$outfile' : $!";
  }else{
	open (OUT, '>', $outfile) or die "Could not write to '$outfile'\n";
  }
  
  my $index = 1;
  while(<IN>){
    if(/^@/){    #Remove SAM header lines
      next;
    }
    
    my $readF = $_;
    my $readR = scalar <IN>;

    my ($chromosomeF, $positionF, $strandF) = get_csome_position($readF);
    my ($chromosomeR, $positionR, $strandR) = get_csome_position($readR);
  
    print OUT "$index\t$chromosomeF\t$positionF\t$strandF\t$chromosomeR\t$positionR\t$strandR\n";
    $index++;
  }

  close IN or die "Could not close filehandle on '$file' : $!";
  close OUT or die "Could not close filehandle on '$outfile' : $!";
}

print "Processing complete\n";

exit (0);


__DATA__

HiCUP homepage: www.bioinformatics.babraham.ac.uk/projects/hicup

The hicup2homer script converts HiCUP BAM/SAM files to a format compatible 
with Homer (Homer Hi-C pages found at: http://homer.salk.edu/homer/interactions).

SYNOPSIS

hicup2homer [OPTIONS]
hicup2homer [SAM/BAM FILES]...

FUNCTION

HiCUP generates SAM/BAM files of mapped, filtered paired-end reads 
constituting the sequenced valid Hi-C di-tags. These may then be analysed by a 
variety of specialised tools, but before this is possible the datasets will 
need parsing into an appropriate format.

The hicup2homer script converts HiCUP BAM/SAM files to a format compatible with 
Homer i.e. 7 columns, with read pairs on the same line:

Column1: index number (integer 1, 2, 3,..)
Column2: forward read chromosome name
Column3: forward read position
Column4: forward read strand (0 = positive strand, 1 = negative strand)
Column5: reverse read chromosome name 
Column6: reverse read position
Column7: reverse read strand (0 = positive strand, 1 = negative strand)

COMMAND LINE OPTIONS

--help         Print help message and exit
--version      Print the program version and exit
--zip          Write output to a gzip file

Full instructions on running the pipeline can be found at:
www.bioinformatics.babraham.ac.uk/projects/hicup

Steven Wingett, Babraham Institute, Cambridge, UK (steven.wingett@babraham.ac.uk)
